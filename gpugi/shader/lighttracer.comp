#version 440

#include "stdheader.glsl"

#ifdef SAVE_LIGHT_CACHE
	#include "lightcache.glsl"
#else
	layout(binding = 4, std140) uniform LightPathTrace
	{
		int NumRaysPerLightSample;
		float LightRayPixelWeight; // (Total num pixels) / (Total num light rays) 
	};
	#define NUM_SAMPLING_TECHNIQUES 1
#endif


// The problem: There is no atomic vec4 operation, even with GL_NV_shader_atomic_float
// The solution: (Spin)Locks for every output pixel. (http://stackoverflow.com/questions/10652862/glsl-semaphores)
// A possible alternative solution: Gather all events in an "append buffer" and apply them via classic vertex scattering (point rendering) and blending

// coherent is necessary!
layout(binding = 0, rgba32f) coherent uniform image2D OutputTexture;
layout(binding = 1, r32ui) coherent uniform uimage2D LockTexture;


#ifdef SAVE_LIGHT_CACHE
	#define MAX_PATHLENGTH 8
#else
	#define MAX_PATHLENGTH 16
#endif

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
void main()
{
#ifdef SAVE_LIGHT_CACHE
	// Not entirely sure if this is 100% reliable!
	// Idea is to keep the counter on GPU without any CPU interaction.
	// Problems could arise if a thread uses "NumLightCacheEntries" before Thread0 wrote this number.
	// This may happen if the GPU does not schedule block 0 as first.
	if(gl_GlobalInvocationID.x == 0)
		NumLightCacheEntries = NumRaysPerLightSample * NumInitialLightSamples;
	memoryBarrierBuffer();
#endif

	uint randomSeed = InitRandomSeed(FrameSeed, gl_GlobalInvocationID.x);
	int lightSampleIndex = int(gl_GlobalInvocationID.x / NumRaysPerLightSample);

	Ray ray;
	vec4 lightSamplePos_Norm0 = texelFetch(InitialLightSampleBuffer, lightSampleIndex * 2);
	vec4 lightFlux_Norm1 = texelFetch(InitialLightSampleBuffer, lightSampleIndex * 2 + 1);
	vec3 rayFlux = lightFlux_Norm1.xyz * LightRayPixelWeight;
	ray.Origin = lightSamplePos_Norm0.xyz;

	vec3 normal = UnpackNormal(vec2(lightSamplePos_Norm0.w, lightFlux_Norm1.w));
	vec3 U, V;
	CreateONB(normal, U, V);
	ray.Direction = SampleUnitHemisphere(Random2(randomSeed), U, V, normal);

#ifdef SAVE_LIGHT_CACHE
	// recursive MIS construction.
	float pathProbability = 1.0;
	float anyPathProbabilitySum = 0.0;

	// Correct init of "NumLightCacheEntries" required!
	// Each thread writes its own initial light sample into the LightCache
	LightCacheEntries[gl_GlobalInvocationID.x].Intensity = rayFlux / PI_2;
	LightCacheEntries[gl_GlobalInvocationID.x].MaterialIndex = -1; // todo
	LightCacheEntries[gl_GlobalInvocationID.x].Position = ray.Origin;
	LightCacheEntries[gl_GlobalInvocationID.x].Normal0 = lightSamplePos_Norm0.w;
	LightCacheEntries[gl_GlobalInvocationID.x].Normal1 = lightFlux_Norm1.w;
	LightCacheEntries[gl_GlobalInvocationID.x].PathProbability = pathProbability;
	LightCacheEntries[gl_GlobalInvocationID.x].AnyPathProbabilitySum = anyPathProbabilitySum;
	//LightCacheEntries[gl_GlobalInvocationID.x].Texcoord // texcoord empty
	//LightCacheEntries[gl_GlobalInvocationID.x].IncidentDirection // direction empty
#endif

	for(int i=0; i<MAX_PATHLENGTH; ++i)
	{
		// Trace ray.
		Triangle triangle;
		vec3 barycentricCoord;
		float rayHit = RAY_MAX;
		TraceRay(ray, rayHit, barycentricCoord, triangle);
		if(rayHit == RAY_MAX)
			break;
		ray.Origin = ray.Origin + rayHit * ray.Direction;	// Go to surface with recursive ray (no epsilon yet!)

		// Compute hit normal and texture coordinate.
		vec3 hitNormal; vec2 hitTexcoord;
		GetTriangleHitInfo(triangle, barycentricCoord, hitNormal, hitTexcoord);

		// Get Material infos.
		MaterialTextureData materialTexData = SampleMaterialData(triangle.w, hitTexcoord);

		// Save light cache entry.
#ifdef SAVE_LIGHT_CACHE
		uint lightCacheEntryIndex = atomicAdd(NumLightCacheEntries, 1);
		if(lightCacheEntryIndex < LightCacheCapacity)
		{
			LightCacheEntries[lightCacheEntryIndex].Intensity = rayFlux / PI_2;
			LightCacheEntries[lightCacheEntryIndex].MaterialIndex = triangle.w;
			// Use "true" position - necessary epsilon can be easily applied in pathtracer_bidir.comp
			LightCacheEntries[lightCacheEntryIndex].Position = ray.Origin; 
			vec2 packedNormal = PackNormal(hitNormal);
			LightCacheEntries[lightCacheEntryIndex].Normal0 = packedNormal.x;
			LightCacheEntries[lightCacheEntryIndex].Normal1 = packedNormal.y;
			LightCacheEntries[lightCacheEntryIndex].Texcoord = hitTexcoord;
			LightCacheEntries[lightCacheEntryIndex].IncidentDirection = ray.Direction;
			LightCacheEntries[lightCacheEntryIndex].PathProbability = pathProbability;
			LightCacheEntries[lightCacheEntryIndex].AnyPathProbabilitySum = anyPathProbabilitySum;
		}
#endif

		// Try to connect to camera.
		Ray cameraRay;
		cameraRay.Direction = CameraPosition - ray.Origin;
		float camViewDotRay = -dot(CameraW, cameraRay.Direction);
		if(camViewDotRay > 0.0) // Camera faces point?
		{
			// Compute screen coord for this ray.
			vec3 proj = vec3(-dot(CameraU, cameraRay.Direction), -dot(CameraV, cameraRay.Direction), camViewDotRay);
			vec2 screenCoord = proj.xy / proj.z;
			screenCoord.x /= dot(CameraU,CameraU);
			screenCoord.y /= dot(CameraV,CameraV);

			// Valid screencoors
			if(screenCoord.x >= -1.0 && screenCoord.x < 1.0 && screenCoord.y > -1.0 && screenCoord.y <= 1.0)
			{
				float cameraDistSq = dot(cameraRay.Direction, cameraRay.Direction);
				float cameraDist = sqrt(cameraDistSq);
				cameraRay.Direction /= cameraDist;
				cameraRay.Origin = cameraRay.Direction * RAY_HIT_EPSILON + ray.Origin;

				// Hits *pinhole* camera
				if(!TraceRayAnyHit(cameraRay, cameraDist))
				{
					// Compute pdf conversion factor from image plane area to surface area.
					// The problem: A pixel sees more than only a point! -> photometric distance law (E = I cosTheta / dÂ²) not applicable!
					// -> We need the solid angle and the projected area of the pixel in this point!
					// Formulas explained in (6.2) http://www.maw.dk/3d_graphics_projects/downloads/Bidirectional%20Path%20Tracing%202009.pdf
					// Also a look into SmallVCM the is a good idea!
					
					// The distance to the image plane  is length(CameraW) == 1.
					// 1/cos == length(pixelPos)
					// vec3 pixelPos = CameraU * screenCoord.x + CameraV * screenCoord.y + CameraW;
					
					float cosAtCameraPow4 = saturate(camViewDotRay / cameraDist); // = dot(-cameraRay.Direction, CameraW);
					//cosAtCameraPow4 *= cosAtCameraPow4; // Not entirely clear why this is more correct in empiric comparisions
					//cosAtCameraPow4 *= cosAtCameraPow4;
					float fluxToRadiance = 1.0 / (cosAtCameraPow4 * PixelArea * cameraDistSq);
					float fluxToIrradiance = fluxToRadiance * saturate(dot(cameraRay.Direction, hitNormal));

					// To hide hide errors in helmholtz reciprocy, trace ray "from camera to light" (instead of in=ray.Direction out=cameraRay.Direction)
					vec3 brdf = BRDF(-cameraRay.Direction, -ray.Direction, triangle.w, materialTexData, hitNormal);

				#ifdef SAVE_LIGHT_CACHE
					float connectionPropability = GetLuminance(brdf);

				#ifdef RUSSIAN_ROULETTE
					const float connectedPathPropability = 1.0;
				#else
					float connectedPathPropability = pathProbability + DIVISOR_EPSILON;
				#endif
					float mis = connectedPathPropability / (connectionPropability * anyPathProbabilitySum + connectedPathPropability);
					mis /= NUM_SAMPLING_TECHNIQUES;

					vec3 color = rayFlux * (fluxToIrradiance * mis) * brdf;
				#else
					vec3 color = rayFlux * fluxToIrradiance * brdf;
				#endif
					

					// Add Sample.
					ivec2 pixelCoord = ivec2((screenCoord*0.5 + vec2(0.5)) * BackbufferSize);

					// Lock pixel - there is a lot you can do wrong!
					// * ..due to shared command counter
					// * ..due to buggy glsl optimizations
					// -> Good overview: http://stackoverflow.com/questions/21538555/broken-glsl-spinlock-glsl-locks-compendium
					bool hasWritten = false; // Do not use break; instead. There's a bug in the GLSL optimizer!
					do {
						if(imageAtomicCompSwap(LockTexture, pixelCoord, uint(0), uint(1)) == 0)
						{
							memoryBarrierImage();

							// Add sample
							vec4 imageVal = imageLoad(OutputTexture, pixelCoord);
							imageStore(OutputTexture, pixelCoord, vec4(imageVal.xyz + color, imageVal.w));
							
							// Release Lock
							imageAtomicExchange(LockTexture, pixelCoord, 0);
							hasWritten = true;
						}
					} while(!hasWritten);
				}
			}
		}

		float connectionPropability;
		//if(i+1 < MAX_PATHLENGTH) // Not using is faster on GK104 on 18.11.14
		if(!ContinuePath(rayFlux, ray, randomSeed, connectionPropability, hitNormal, triangle.w, materialTexData))
			break;

	#ifdef SAVE_LIGHT_CACHE
		connectionPropability = MIS(connectionPropability);
		anyPathProbabilitySum = anyPathProbabilitySum * connectionPropability + pathProbability;
		pathProbability *= connectionPropability;
	#endif
	}
}