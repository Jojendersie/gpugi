#version 440

#include "stdheader.glsl"
#ifdef SAVE_PIXEL_CACHE
	#include "pixelcache.glsl"
#endif

layout(binding = 0, rgba32f) restrict uniform image2D OutputTexture; // attention! No coherent flag set!

#define NUM_LIGHT_SAMPLES 2
#define MAX_PATHLENGTH 8

#define LOCAL_SIZE 8
layout (local_size_x = LOCAL_SIZE, local_size_y = LOCAL_SIZE, local_size_z = 1) in;
void main()
{
	const float lightSampleWeight = float(NumInitialLightSamples) / NUM_LIGHT_SAMPLES; // This could be a uniform!

	ivec2 gridPosition = ivec2(gl_GlobalInvocationID.xy);
	int gridIndex = gridPosition.x + gridPosition.y * BackbufferSize.x;
	uint randomSeed = InitRandomSeed(FrameSeed, gridIndex);
	vec2 screenCoord = (Random2(randomSeed) + gridPosition) / BackbufferSize * 2.0 - 1.0; // Random2 gives [0,1[. Adding [0, BackbufferSize[ should result in [0, BackbufferSize]

	Ray ray;
	ray.Origin = CameraPosition;
	ray.Direction = normalize(screenCoord.x*CameraU + screenCoord.y*CameraV + CameraW);

	vec3 color = vec3(0.0);
	vec3 pathThroughput = vec3(1.0);
	// If we could have decided for the diffuse case in the last step, then the direct
	// illumination specular part is already contained. This needs to be masked.
	bool lastVertexDiffusePossible = false;
	
#ifdef SAVE_PIXEL_CACHE
	// Not sure if the loop will ever write something to the buffer
	// -> make sure that it has no influence later
	PixelCacheEntries[gridIndex].PathThroughput = vec3(0.0);
#endif

	for(int i=0; i<MAX_PATHLENGTH; ++i)
	{
		// Trace ray.
		Triangle triangle;
		vec3 barycentricCoord;
		float rayHit = RAY_MAX;
		TraceRay(ray, rayHit, barycentricCoord, triangle);
		if(rayHit == RAY_MAX)
			break;
		ray.Origin = ray.Origin + rayHit * ray.Direction;	// Go to surface with recursive ray (no epsilon yet!)

		// Compute hit normal and texture coordinate.
		vec3 hitNormal; vec2 hitTexcoord;
		GetTriangleHitInfo(triangle, barycentricCoord, hitNormal, hitTexcoord);
		// Get Material infos.
		MaterialTextureData materialTexData = SampleMaterialData(triangle.w, hitTexcoord);
		
		// Add self emissivity of hit material.
		if(!lastVertexDiffusePossible)
		{
			vec3 emissivity = vec3(Materials[triangle.w].emissivityRG, Materials[triangle.w].emissivityB);
			color += pathThroughput * emissivity;
		}

		// Make a decision
		vec3 pdiffuse = GetDiffuseProbability(triangle.w, materialTexData, abs(dot(hitNormal, ray.Direction)));
		float pdiffuseAvg = GetLuminance(pdiffuse);
		lastVertexDiffusePossible = pdiffuseAvg > 0.0;
		//if(pdiffuseAvg >= Random(seed) && i==0)
		// Sample first to make a decision (next event or iterate)
		vec3 throughput = vec3(1.0);
		vec3 sampleDirection = SampleBSDF(ray.Direction, triangle.w, materialTexData, randomSeed, hitNormal, throughput);
		if(throughput.x == -1.0) // -1 marks the end of the path (diffuse decision) for the Whitted sampler
		{
			// Connect to random light samples ("Next Event Estimation")
			Ray lightRay;
			for(int lightSample=0; lightSample<NUM_LIGHT_SAMPLES; ++lightSample)
			{
			#ifdef SHOW_SPECIFIC_PATHLENGTH
				if(i+1 != SHOW_SPECIFIC_PATHLENGTH)
					break;
			#endif

				int sampleIndex = int(RandomUInt(randomSeed) % NumInitialLightSamples);
				vec4 lightSamplePos_Norm0 = texelFetch(InitialLightSampleBuffer, sampleIndex * 2);
				vec4 lightIntensity_Norm1 = texelFetch(InitialLightSampleBuffer, sampleIndex * 2 + 1);
				
				// Direction to light & distance.
				lightRay.Direction = lightSamplePos_Norm0.xyz - ray.Origin;
				float lightDistSq = dot(lightRay.Direction, lightRay.Direction) + DIVISOR_EPSILON;
				float lightDist = sqrt(lightDistSq);
				lightRay.Direction /= lightDist;

				// Facing the light?
				vec3 lightSampleDir = UnpackNormal(vec2(lightSamplePos_Norm0.w, lightIntensity_Norm1.w));
				float lightSampleIntensityFactor = saturate(dot(lightSampleDir, -lightRay.Direction));
				if(lightSampleIntensityFactor > 0.0)
				{
					lightRay.Origin = RAY_HIT_EPSILON * lightRay.Direction + ray.Origin;

					if(!TraceRayAnyHit(lightRay, lightDist))
					{
						// Hemispherical lambert emitter. First we want intensity, not radiance
						vec3 lightSampleIntensity = lightIntensity_Norm1.xyz * lightSampleIntensityFactor; // lightIntensity = lightIntensity in normal direction (often called I0) -> seen area is smaller to the border
						vec3 bsdf = BSDF(ray.Direction, lightRay.Direction, triangle.w, materialTexData, hitNormal);

						vec3 irradiance = (saturate(dot(lightRay.Direction, hitNormal)) / lightDistSq) * lightSampleIntensity; // Use saturate, otherwise light from behind may come in because of shading normals!
						color += irradiance * pathThroughput * bsdf * (lightSampleWeight / (pdiffuseAvg + DIVISOR_EPSILON));
					}
				}
			}
			
		#ifdef SAVE_PIXEL_CACHE
			if(i==0) break;
			// Save from where we came and the information for the current hit point
			PixelCacheEntries[gridIndex].PathThroughput = pathThroughput;
			PixelCacheEntries[gridIndex].Diffuse = PackR11G11B10(materialTexData.Diffuse);
			PixelCacheEntries[gridIndex].Vertex = ray.Origin;
			PixelCacheEntries[gridIndex].Normal01 = PackNormal(hitNormal);
			PixelCacheEntries[gridIndex].Incident = ray.Direction; // TODO is the sign correct?
			PixelCacheEntries[gridIndex].Shininess = materialTexData.Reflectiveness.w;
			PixelCacheEntries[gridIndex].Reflectiveness = PackR11G11B10(materialTexData.Reflectiveness.rgb);
			PixelCacheEntries[gridIndex].Opacity = PackR11G11B10(materialTexData.Opacity);
			PixelCacheEntries[gridIndex].MaterialID = triangle.w;
		#endif
			break;
		}
		// Tracer further
		ray.Direction = sampleDirection;
		ray.Origin += ray.Direction * RAY_HIT_EPSILON;
		// Color shift only
		pathThroughput *= throughput / (GetLuminance(throughput) + DIVISOR_EPSILON);
	}
	
	vec4 final = vec4(color, 1.0);
	final += imageLoad(OutputTexture, gridPosition);
	imageStore(OutputTexture, gridPosition, final);
}