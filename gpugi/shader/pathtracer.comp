#version 440

#include "stdheader.glsl"


layout(binding = 0, rgba32f) restrict uniform image2D OutputTexture; // attention! No coherent flag set!

#define NUM_LIGHT_SAMPLES 1
#define MAX_PATHLENGTH 16

#define LOCAL_SIZE 8
layout (local_size_x = LOCAL_SIZE, local_size_y = LOCAL_SIZE, local_size_z = 1) in;
void main()
{
	ivec2 gridPosition = ivec2(gl_GlobalInvocationID.xy);
	uint randomSeed = InitRandomSeed(FrameSeed, gridPosition.x + gridPosition.y * BackbufferSize.x);
	vec2 screenCoord = (Random2(randomSeed) + gridPosition) / BackbufferSize * 2.0 - 1.0; // Random2 gives [0,1[. Adding [0, BackbufferSize[ should result in [0, BackbufferSize]

	Ray ray;
	ray.Origin = CameraPosition;
	ray.Direction = normalize(screenCoord.x*CameraU + screenCoord.y*CameraV + CameraW);

	vec3 color = vec3(0.0);
	vec3 pathThroughput = vec3(1.0);

	const float lightSampleWeight = float(NumInitialLightSamples) / NUM_LIGHT_SAMPLES; // This could be a uniform!

	for(int i=0; i<MAX_PATHLENGTH; ++i)
	{
		// Trace ray.
		Triangle triangle;
		vec3 barycentricCoord;
		float rayHit = RAY_MAX;
		TraceRay(ray, rayHit, barycentricCoord, triangle);
		if(rayHit == RAY_MAX)
			break;

		// Compute hit normal and texture coordinate.
		vec3 hitNormal; vec2 hitTexcoord;
		GetTriangleHitInfo(triangle, barycentricCoord, hitNormal, hitTexcoord);
		// Get Material infos.
		MaterialTextureData materialTexData = SampleMaterialData(triangle.w, hitTexcoord);

		// Connect to random light samples ("Next Event Estimation")
		Ray lightRay;
		ray.Origin = ray.Origin + rayHit * ray.Direction;	// Go to surface with recursive ray (no epsilon yet!)
		for(int lightSample=0; lightSample<NUM_LIGHT_SAMPLES; ++lightSample)
		{
			int sampleIndex = int(RandomUInt(randomSeed) % NumInitialLightSamples);
			vec3 lightSamplePos = texelFetch(InitialLightSampleBuffer, sampleIndex * 2).xyz;

			lightRay.Direction = lightSamplePos - ray.Origin;
			float lightDistSq = dot(lightRay.Direction, lightRay.Direction);
			float lightDist = sqrt(lightDistSq);
			lightRay.Direction /= lightDist + DIVISOR_EPSILON;
			rayHit = lightDist;
			lightRay.Origin = RAY_HIT_EPSILON * lightRay.Direction + ray.Origin;

			if(!TraceRayAnyHit(lightRay, rayHit))
			{
				vec3 lightSampleIntensity = texelFetch(InitialLightSampleBuffer, sampleIndex * 2 + 1).xyz;
				vec3 bsdf = BSDF(ray.Direction, lightRay.Direction, triangle.w, materialTexData, hitNormal);

				// Add Sample (L): L(hitPoint, lightRay.Direction) * bsdf * dot(lightRay.Direction, hitNormal)
				// L(hitPoint, lightRay.Direction) = lightSampleIntensity / lightDistSq

				vec3 irradiance = (lightSampleWeight *		// Divide by p for Montecarlo
								saturate(dot(lightRay.Direction, hitNormal)) / lightDistSq) * lightSampleIntensity;
				color += irradiance * pathThroughput * bsdf;
			}
		}

		//if(i+1 < MAX_PATHLENGTH) // Not using is faster on GK104 on 18.11.14
		ray.Direction = SampleBSDF(ray.Direction, triangle.w, materialTexData, randomSeed, hitNormal, pathThroughput);
		ray.Origin += ray.Direction * RAY_HIT_EPSILON;
	#ifdef RUSSIAN_ROULETTE
		float terminationPropability = GetLuminance(pathThroughput);
		if(Random(randomSeed) >= terminationPropability) // if connectionPropability is zero, path should be stoped -> >=
			break;
		pathThroughput /= terminationPropability; // Only change in spectrum, no energy loss.
	#endif
	}

	vec4 final = vec4(color, 1.0);
	final += imageLoad(OutputTexture, gridPosition);
	imageStore(OutputTexture, gridPosition, final);
}