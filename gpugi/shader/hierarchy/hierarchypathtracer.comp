#version 450

#define HIT_INDEX_MASKING
#include "../stdheader.glsl"
#include "sggx.glsl"

layout(binding=7) uniform samplerBuffer SGGXBuffer;
layout(binding=8) uniform samplerBuffer HierarchyMaterialBuffer;

layout(binding = 0, rgba32f) restrict uniform image2D OutputTexture; // attention! No coherent flag set!

#define NUM_LIGHT_SAMPLES 1
#define MAX_PATHLENGTH 3

#define LOCAL_SIZE 8
layout (local_size_x = LOCAL_SIZE, local_size_y = LOCAL_SIZE, local_size_z = 1) in;
void main()
{
	const float lightSampleWeight = float(NumInitialLightSamples) / NUM_LIGHT_SAMPLES; // This could be a uniform!

	ivec2 gridPosition = ivec2(gl_GlobalInvocationID.xy);
	uint randomSeed = InitRandomSeed(FrameSeed, gridPosition.x + gridPosition.y * BackbufferSize.x);
	vec2 screenCoord = (Random2(randomSeed) + gridPosition) / BackbufferSize * 2.0 - 1.0; // Random2 gives [0,1[. Adding [0, BackbufferSize[ should result in [0, BackbufferSize]

	Ray ray;
	ray.Origin = CameraPosition;
	ray.Direction = normalize(screenCoord.x*CameraU + screenCoord.y*CameraV + CameraW);

	vec3 color = vec3(0.0);
	vec3 pathThroughput = vec3(1.0);
	bool didNextEventEstimation = false;
	ivec2 hitMask = ivec2(0xFFFFFFFF);

	for(int i=0; i<MAX_PATHLENGTH; ++i)
	{
		// Trace ray.
		Triangle triangle;
		vec3 barycentricCoord;
		float rayHit = RAY_MAX;
		TraceRay(ray, rayHit, hitMask, barycentricCoord, triangle);
		if(rayHit == RAY_MAX)
			break;
		ray.Origin = ray.Origin + rayHit * ray.Direction;	// Go to surface with recursive ray (no epsilon because of masking!)

		// Get Material infos and normal either from node or from triangle hit point.
		vec3 hitNormal;
		float epsilon = RAY_HIT_EPSILON;
		MaterialData materialData;
		if(hitMask.y == 0xFFFFFFFF)
		{
			// Unpack material from hierarchy buffer
			materialData.Reflectiveness = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6);
			materialData.Opacity = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6 + 1).xyz;
			materialData.Diffuse = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6 + 2).xyz;
			materialData.Emissivity = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6 + 3).xyz;
			vec4 tmp = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6 + 4);
			materialData.Fresnel0 = tmp.xyz;
			materialData.RefractionIndexAvg = tmp.w;
			materialData.Fresnel1 = texelFetch(HierarchyMaterialBuffer, hitMask.x * 6 + 5).xyz;
			// Load and unpack the symmetric SGGX matrix
			mat3x3 sggx;
			tmp.xy = texelFetch(SGGXBuffer, hitMask.x * 3 + 1).xy;
			vec3 sigma = vec3(texelFetch(SGGXBuffer, hitMask.x * 3).xy, tmp.x);
			vec3 r = vec3(tmp.y, texelFetch(SGGXBuffer, hitMask.x * 3 + 2).xy) * 2.0 - 1.0;
			sggx[0][0] = saturate(sigma.x * sigma.x);
			sggx[1][1] = saturate(sigma.y * sigma.y);
			sggx[2][2] = saturate(sigma.z * sigma.z);
			sggx[0][1] = sggx[1][0] = clamp(r.x * sigma.x * sigma.y, -1.0, 1.0);
			sggx[0][2] = sggx[2][0] = clamp(r.y * sigma.x * sigma.z, -1.0, 1.0);
			sggx[1][2] = sggx[2][1] = clamp(r.z * sigma.y * sigma.z, -1.0, 1.0);
			hitNormal = sampleNormal(ray.Direction, sggx, randomSeed);
			epsilon = length(texelFetch(HierachyBuffer, hitMask.x * 2).xyz -
							 texelFetch(HierachyBuffer, hitMask.x * 2 + 1).xyz);
		} else {
			// Compute normal and texture coordinate from barycentric coordinates
			vec2 hitTexcoord;
			GetTriangleHitInfo(triangle, barycentricCoord, hitNormal, hitTexcoord);
			// Sample material
			materialData = SampleMaterialData(triangle.w, hitTexcoord);
		}

		// Add self emissivity of hit material.
		// To avoid double counting use either next event estimation or this sampling. In general,
		// next event estimation performs better, but for specular bounces or direct view sampling
		// is more probable to find the correct light.
		if( !didNextEventEstimation )
			color += pathThroughput * materialData.Emissivity;
		
		// Connect to random light samples ("Next Event Estimation")
		didNextEventEstimation = materialData.Reflectiveness.w < 500.0;
		if( didNextEventEstimation )
		{
			Ray lightRay;
			for(int lightSample=0; lightSample<NUM_LIGHT_SAMPLES; ++lightSample)
			{
			#ifdef SHOW_SPECIFIC_PATHLENGTH
				if(i+1 != SHOW_SPECIFIC_PATHLENGTH)
					break;
			#endif

				int sampleIndex = int(RandomUInt(randomSeed) % NumInitialLightSamples);
				vec4 lightSamplePos_Norm0 = texelFetch(InitialLightSampleBuffer, sampleIndex * 2);
				vec4 lightIntensity_Norm1 = texelFetch(InitialLightSampleBuffer, sampleIndex * 2 + 1);
				
				// Direction to light & distance.
				lightRay.Direction = lightSamplePos_Norm0.xyz - ray.Origin;
				float lightDistSq = dot(lightRay.Direction, lightRay.Direction);
				float lightDist = sqrt(lightDistSq);
				lightRay.Direction /= lightDist;

				// Facing the light?
				vec3 lightSampleDir = UnpackNormal(vec2(lightSamplePos_Norm0.w, lightIntensity_Norm1.w));
				float lightSampleIntensityFactor = saturate(dot(lightSampleDir, -lightRay.Direction));
				if(lightSampleIntensityFactor > 0.0)
				{
					lightRay.Origin = ray.Origin + lightRay.Direction * epsilon;

					if(hitMask.y != 0xFFFFFFFF)
						hitMask.x = 0xFFFFFFFF;
					if(!TraceRayAnyHit(lightRay, lightDist - RAY_HIT_EPSILON - epsilon, hitMask))
					{
						// Hemispherical lambert emitter. First we want intensity, not radiance
						vec3 lightSampleIntensity = lightIntensity_Norm1.xyz * lightSampleIntensityFactor; // lightIntensity = lightIntensity in normal direction (often called I0) -> seen area is smaller to the border
						vec3 bsdf = BSDF(ray.Direction, lightRay.Direction, materialData, hitNormal);

						// Add Sample (L): L(hitPoint, lightRay.Direction) * bsdf * dot(lightRay.Direction, hitNormal)
						// L(hitPoint, lightRay.Direction) = lightSampleIntensity / lightDistSq

						vec3 irradiance = (saturate(dot(lightRay.Direction, hitNormal)) / lightDistSq) * lightSampleIntensity; // Use saturate, otherwise light from behind may come in because of shading normals!
						color += irradiance * (pathThroughput * lightSampleWeight) * bsdf;
						//color += min(irradiance * pathThroughput * bsdf, 1); // <- surpress fire flies
					}
				}
			}
		}

		//if(i+1 < MAX_PATHLENGTH) // Not using is faster on GK104 on 18.11.14
		vec3 throughput = vec3(1.0);
		ray.Direction = SampleBSDF(ray.Direction, materialData, randomSeed, hitNormal, throughput);
		ray.Origin = ray.Origin + ray.Direction * epsilon;
		pathThroughput *= throughput;
	#ifdef RUSSIAN_ROULETTE
		float continuationPropability = saturate(GetLuminance(throughput));
		if(Random(randomSeed) >= continuationPropability) // if continuationPropability is zero, path should be stoped -> >=
			break;
		pathThroughput /= continuationPropability; // Only change in spectrum, no energy loss.
	#endif
	}

	vec4 final = vec4(color, 1.0);
	final += imageLoad(OutputTexture, gridPosition);
	imageStore(OutputTexture, gridPosition, final);
}