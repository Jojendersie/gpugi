#version 450

#include "../stdheader.glsl"
#include "importanceubo.glsl"

layout(binding = 0, std430) restrict buffer HierarchyImportanceBuffer
{
	float HierarchyImportance[];
};
layout(binding = 10, std430) restrict buffer SubtreeImportanceBuffer
{
	float SubtreeImportance[];
};

layout(binding = 6) uniform samplerBuffer ParentPointerBuffer;

// Value used to notify parent nodes that child is done with gathering importance.
const float NotificationBias = 1.19e-07; // Float machine epsilon

// Should be called once per inner node.
layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
void main()
{
	// If there is no inner node for this thread, return. (May happen due to block sizing)
	if(gl_GlobalInvocationID.x >= NumInnerNodes)
		return;

	// Get node info (see also "../traceray.glsl")
	uint childCodeRaw = floatBitsToUint(texelFetch(HierachyBuffer, int(gl_GlobalInvocationID.x) * 2).w);
	int childNodeIndex = int(childCodeRaw & uint(0x7FFFFFFF));	// Most significant bit tells us is this is a leaf.
	bool isChildLeaf = childNodeIndex != childCodeRaw;

	float totalImportance = HierarchyImportance[gl_GlobalInvocationID.x];
	float childrenImportance = 0.0;

	// Sum up importance values of the children (triangles or nodes).
	if(isChildLeaf)
	{
		int currentTriangleIndex = int(TRIANGLES_PER_LEAF * childNodeIndex);
		for(int i=0; i<TRIANGLES_PER_LEAF; ++i, ++currentTriangleIndex)
		{
			childrenImportance += HierarchyImportance[NumInnerNodes + currentTriangleIndex];
		}

		// Amplify the importance by the ratio of children and own importance.
		// This is the hit-probability-difference between the nodes. The higher the difference the
		// more important is to check the children themselves.
		totalImportance *= totalImportance / childrenImportance;
		// Normalize (each individual importance value should have been divided by the number
		// of iterations, which is equivalent to doing it once in the end).
		HierarchyImportance[gl_GlobalInvocationID.x] = totalImportance / NumImportanceIterations;
		SubtreeImportance[gl_GlobalInvocationID.x] = max(childrenImportance, NotificationBias);
	}
}
