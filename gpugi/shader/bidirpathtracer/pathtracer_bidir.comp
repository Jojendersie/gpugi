#version 440

#define TRINORMAL_OUTPUT
#include "../stdheader.glsl"
#include "lightcache.glsl"

#ifdef CHAINED_MIS
	#include "chained_mis.glsl"
#endif

layout(binding = 0, rgba32f) restrict uniform image2D OutputTexture; // attention! No coherent flag set!


#define LOCAL_SIZE 8
layout (local_size_x = LOCAL_SIZE, local_size_y = LOCAL_SIZE, local_size_z = 1) in;
void main()
{
	ivec2 gridPosition = ivec2(gl_GlobalInvocationID.xy);
	uint randomSeed = InitRandomSeed(FrameSeed, gridPosition.x + gridPosition.y * BackbufferSize.x);
	vec2 screenCoord = (Random2(randomSeed) - 0.5 + gridPosition) / BackbufferSize * 2.0 - 1.0; // Random2 gives [0,1[. Adding [0, BackbufferSize[ should result in [0, BackbufferSize]

	Ray ray;
	ray.Origin = CameraPosition;
	ray.Direction = normalize(screenCoord.x*CameraU + screenCoord.y*CameraV + CameraW);

	vec3 color = vec3(0.0);
	vec3 pathThroughput = vec3(1.0);
	bool didNextEventEstimation = false;

	uint actualNumLightCacheEntries = min(NumLightCacheEntries, LightCacheCapacity);
	float lightSampleWeight = float(actualNumLightCacheEntries);

	// Recursive MIS construction.
	// Whenever we want to compute the MIS weight for a path segment we need the probabilities
	// on both vertices. Therefore we need to remember the last probability from our path.
	// The following heuristic weights between projection and path tracing.
	// There are 3 different techniques: (weights)
	//    - LPT-projection:        (1-a)
	//    - PT-random hit:         (a) * Product(!nee,0,i)
	//    - Next-event estimation: (a) * Product(!nee,0,i-1) * nee
	// Where Product(!nee,0,i) is the combined weight of of all previous vertices where
	// we did not made a next event estimation (i.e. all samplings on the current path).
	// Additional to PDF based importance weights p0 / (p0+p1+...) we use some distance
	// based measure to decide between NEE and Sampling. If another surface is very close
	// Sampling is preferred, because direct illumination creates very bright spots in that case.
	float lastConnectionProbability = InitialMIS();
	float lastCosAbs = 1.0;
	float mis = 0.0;
#ifdef CHAINED_MIS
	for(int i=0; i<MAX_PATHLENGTH; ++i)
		g_mis[i] = 0.0;
#endif

	for(int i=0; i<MAX_PATHLENGTH; ++i)
	{
		// Trace ray.
		Triangle triangle;
		vec3 barycentricCoord;
		float rayHit = RAY_MAX;
		vec3 geometryNormal;
		TraceRay(ray, rayHit, barycentricCoord, triangle, geometryNormal);
		if(rayHit == RAY_MAX)
			break;
		geometryNormal = normalize(geometryNormal);

		ray.Origin = ray.Origin + rayHit * ray.Direction;	// Go to surface with recursive ray (no epsilon yet!)

		// Compute hit normal and texture coordinate.
		vec3 hitNormal; vec2 hitTexcoord;
		GetTriangleHitInfo(triangle, barycentricCoord, hitNormal, hitTexcoord);
		//hitNormal = geometryNormal;
		// Get Material infos.
		MaterialData materialData = SampleMaterialData(triangle.w, hitTexcoord);
		
	#ifdef SHOW_SPECIFIC_PATHLENGTH
		if(i == SHOW_SPECIFIC_PATHLENGTH)
		{
	#endif
			// If we hit an emissive material we have a full path-tracer sample.
			// Need to compute the previous path segment weight based on the hit information.
			// If i==0 we have a path which is never projected (special case) -> mis=1.
			float incidentSurfaceCosSat = saturate(-dot(hitNormal, ray.Direction));
			float connectionLightToEyePath = DistanceMISHeuristic(rayHit, lastCosAbs) * MISHeuristic(incidentSurfaceCosSat / PI);
			mis = (i == 0) ? 1.0 : (lastConnectionProbability / max(lastConnectionProbability + connectionLightToEyePath, DIVISOR_EPSILON));
		#ifdef CHAINED_MIS
			// mis = qualityWeightedMIS_Trace(mis, 1.0, 1.0); // quality update with 1/1 has no effect
			g_mis[i] = mis;
			mis = cmisPathThroughputWeight(i+1, 1.0, 1.0); // Recursive quality depends on the past and has an effect!
		#endif
			color += mis * pathThroughput * materialData.Emissivity;
	#ifdef SHOW_SPECIFIC_PATHLENGTH
		}
	#endif
	
		// Connect to random light samples ("Next Event Estimation")
		Ray lightRay;
		for(int lightSample=0; lightSample<1; ++lightSample)//AverageLightPathLength
		{
			uint lightCacheSampleIndex = RandomUInt(randomSeed) % actualNumLightCacheEntries;
			int pathLength = LightCacheEntries[lightCacheSampleIndex].PathLength + i + 1;
		#ifdef SHOW_SPECIFIC_PATHLENGTH
			if(pathLength != SHOW_SPECIFIC_PATHLENGTH)
				continue;
		#endif
			// This option helps to compare renderer by avoiding combined path with length
			// [MAX_PATHLENGTH+1, MAX_PATHLENGTH*MAX_PATHLENGTH].
			if(pathLength > MAX_PATHLENGTH)
				continue;
		
			lightRay.Direction = LightCacheEntries[lightCacheSampleIndex].Position - ray.Origin;
			float lightDistSq = dot(lightRay.Direction, lightRay.Direction);
			float lightDist = sqrt(lightDistSq);
			lightRay.Direction /= max(lightDist, DIVISOR_EPSILON);
			lightRay.Origin = RAY_HIT_EPSILON * lightRay.Direction + ray.Origin;

			if(!TraceRayAnyHit(lightRay, lightDist - RAY_HIT_EPSILON*2)) // Important to subtract epsilon, since light sample may be slightly behind the surface
			{
				int lightSampleMaterialIndex = LightCacheEntries[lightCacheSampleIndex].MaterialIndex;
				vec3 lightSampleFlux = LightCacheEntries[lightCacheSampleIndex].Flux;
				vec3 lightSampleHitNormal = vec3(0.0, 1.0, 0.0);
				float connectionLightPathToEyePath;
				float cosLight = 0.0;
				vec3 lightSampleIntensity;
				float cosCamera = abs(dot(lightRay.Direction, hitNormal));

				if(LightCacheEntries[lightCacheSampleIndex].Normal1 < 1.5)
				{
					lightSampleHitNormal = UnpackNormal(LightCacheEntries[lightCacheSampleIndex].Normal0, LightCacheEntries[lightCacheSampleIndex].Normal1);
					cosLight = abs(dot(lightRay.Direction, lightSampleHitNormal));
				}

				float pdf;
				vec3 bsdf = BSDF(ray.Direction, lightRay.Direction, materialData, hitNormal, pdf); // Note that this is also 1/samplingProbability!
				float connectionEyePathToLightPath = MISHeuristic(pdf);

				// Additional to the light-path-segment-MIS we need the MIS for the previous
				// eye-path-segment.
				if(i > 0)
				{
					float distMIS = DistanceMISHeuristic(rayHit, lastCosAbs);
					connectionEyePathToLightPath *= distMIS;
					//mis = connectionEyePathToLightPath / max(lastConnectionProbability + connectionEyePathToLightPath, DIVISOR_EPSILON);
					mis = lastConnectionProbability / max(lastConnectionProbability + connectionEyePathToLightPath, DIVISOR_EPSILON);
					if(materialData.Reflectiveness.w >= 100.0) // On this material there is now light cache -> always trace with weight 1
						mis = 1.0;
				} else
					mis = connectionEyePathToLightPath / max(lastConnectionProbability + connectionEyePathToLightPath, DIVISOR_EPSILON);
										
				float misLight = 1.0;
				if(LightCacheEntries[lightCacheSampleIndex].Normal1 < 1.5) // If Lambertian
				{
					// Triangle material
					if(lightSampleMaterialIndex != -1)
					{
						MaterialData lightSampleMaterialData = SampleMaterialData(lightSampleMaterialIndex, LightCacheEntries[lightCacheSampleIndex].Texcoord);

						vec3 incidentDir = UnpackNormal(LightCacheEntries[lightCacheSampleIndex].IncidentDirectionPacked);
						vec3 lightBSDF = AdjointBSDF(lightRay.Direction, -incidentDir,
												lightSampleMaterialData, lightSampleHitNormal, pdf);

						lightSampleIntensity = lightSampleFlux * lightBSDF * cosLight;
						
						connectionLightPathToEyePath = MISHeuristic(pdf) * DistanceMISHeuristic(lightDist, cosCamera);
					} else {
						lightSampleIntensity = lightSampleFlux * (cosLight * PI_2);// hemispherical light source
						connectionLightPathToEyePath = MISHeuristic(cosLight / PI) * DistanceMISHeuristic(lightDist, cosCamera);
					}
					// MIS for the path segment computed here, because point lights have no MIS.
					misLight = connectionLightPathToEyePath / max(connectionLightPathToEyePath + connectionEyePathToLightPath, DIVISOR_EPSILON);
				}
				// Point light source.
				else
				{
					lightSampleIntensity = lightSampleFlux * (PI_2);// omnidirectional light source
					// Only compute a MIS for area lights and light path vertices.
					// Point lights cannot be hit randomly.
					misLight = 1.0;
				}

				//mis = 0.0;
				// Due to random sampling of light vertices the probability of long paths
				// increases. E.g. the first eye-path vertex can randomly produce paths of
				// the length 1+1+len(light-path), the next vertex of length 2+1+len(light-path), ...
				// To compensate that higher energy we need to divide by the full path length.
		//??		mis /= LightCacheEntries[lightCacheSampleIndex].PathLength + 1 + i;
		
			#ifdef CHAINED_MIS
				float quality_nee = LightCacheEntries[lightCacheSampleIndex].ChainedMISWeightN;
				float quality_trace = LightCacheEntries[lightCacheSampleIndex].ChainedMISWeightT;
				misLight = qualityWeightedMIS_NEE(misLight, quality_nee, quality_trace);
				updateQuality(quality_nee, 1.0 - misLight);
				updateQuality(quality_trace, misLight);
				g_mis[i] = mis;
				mis = cmisPathThroughputWeight(i+1, quality_nee, quality_trace);
				//mis = qualityWeightedMIS_Trace(mis, quality_nee, quality_trace);
				//mis = 1.0-mis;
				//mis = LightCacheEntries[lightCacheSampleIndex].ChainedMISWeightN;
				//mis *= 100.0;
			#endif
				mis *= misLight;

				vec3 irradiance = (mis * lightSampleWeight * cosCamera / lightDistSq) * lightSampleIntensity;
				color += irradiance * pathThroughput * bsdf; 
			//	color = vec3(mis);
			}
		}

		float samplePDF;
		vec3 throughput = vec3(1.0);
		ray.Direction = SampleBSDF(ray.Direction, materialData, randomSeed, hitNormal, throughput, samplePDF);
		ray.Origin += ray.Direction * RAY_HIT_EPSILON;
		pathThroughput *= throughput;
	#ifdef RUSSIAN_ROULETTE
		float continuationPropability = saturate(GetLuminance(throughput));
		if(Random(randomSeed) >= continuationPropability) // if continuationPropability is zero, path should be stoped -> >=
			break;
		pathThroughput /= continuationPropability; // Only change in spectrum, no energy loss.
	#endif
	
		// Update the pathThroughput according to MIS, because we could have created the
		// same path in the reverse direction (with the reverse MIS).
		// The pdf is reciprocal (HOPEFULLY) and therefore is equal to what the light tracer
		// would have done in the inverse direction.
		float reverseConnectionProbability = MISHeuristic(samplePDF);
		if(i == 0) // Special case PT or LPT-projection?
		{
			mis = reverseConnectionProbability / max(reverseConnectionProbability + lastConnectionProbability, DIVISOR_EPSILON);
		} else {
			float distMIS = DistanceMISHeuristic(rayHit, lastCosAbs);
			reverseConnectionProbability *= distMIS;
			//mis = reverseConnectionProbability / max(reverseConnectionProbability + lastConnectionProbability, DIVISOR_EPSILON);
			mis = lastConnectionProbability / max(lastConnectionProbability + distMIS * reverseConnectionProbability, DIVISOR_EPSILON);
			if(materialData.Reflectiveness.w >= 100.0) // On this material there is now light cache -> always trace with weight 1
				mis = 1.0;
		}
		// Do not update pathThroughput in ChainedMIS, because mis changes with the future of the path.
		// Instead store the mis for later correct computation.
	#ifdef CHAINED_MIS
		g_mis[i] = mis;
	#else
		pathThroughput *= mis;
	#endif
		lastConnectionProbability = reverseConnectionProbability;
		lastCosAbs = abs(dot(ray.Direction, hitNormal));
	}

	//color *= 0.0;
	vec4 final = vec4(color, 1.0);
	final += imageLoad(OutputTexture, gridPosition);
	imageStore(OutputTexture, gridPosition, final);
}